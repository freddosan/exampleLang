#id: "org.mal-lang.kvmlang"
#version: "0.0.2"

include "core.mal"

category System {



    asset NovaService extends System
    developer info: "This is the worker deamon that creates or terminates VM's trough libVirt, which further controls QemuKVM" 
    user info: "This is when a user has access to the Nova Comand Line interface (CLI) / Openstack CLI which controls the VM's"
    {


          | fullAccess @hidden
            -> attemptUseCLI

          | _machineAccess @hidden
            -> attemptUseCLI

          & attemptUseCLI
          user info: "the user is attempting to access the CLI"
            ->  Instances.delete, // (admin only --all-tenants)
                Instances.stop //Stop server. (admin only --all-tenants) 
  
    }

     abstract asset LSM extends Object
      developer info: "New asset for KVM-QEMU. Linux security module."
     {
      

     }

    asset SELinux extends LSM
      developer info: "New asset for KVM-QEMU. Security-Enhanced Linux restricts the privileges of the qemu process by establishing security boundaries, so if an attacker would compromise the hypervisor, sVirt restricts the VM's access outside of its boundaries "
     {
        //developer info: "If sVirt is enabled, MAC is enforced to the VM's running on the host. The attack cannot proceed outside of the VM-process boundaries."     
     }
     
    asset QemuKVM extends Object
        developer info: "New asset for KVM-QEMU."
        user info: "Qemu emulates vCpu, SMP, Soft MMU, I &T. Mech, I/O Network, Paravirtualized I/O, VM Exits, Hypercalls, "
        { 
           
            | bufferOverflow
                developer info: "CVE-2015-5158, 2015-7512, 2015-7504, 2017-10806"
                -> guestInstanceDOS

            | outOfBoundsReadORWrite
                developer info: "CVE-2017-11334, CVE-2017-13672, CVE-2017-7718, CVE-2017-15289, CVE-2015-8619, CVE-2016-10029"
                -> guestInstanceDOS

            | nullPointerDereference
               developer info: "CVE-2017-12809"
                ->  guestInstanceDOS

            | guestInstanceDOS
              developer info: "If an instance goes down, this means DenialOfService"
              -> sysExecutedInstances.deny 

            | stop
             developer info: "If an instance is stopped, this means DenialOfService"
             -> sysExecutedInstances.deny  

            | delete
              developer info: "Removal of an instance would result in DOS and loss of data"
              -> sysExecutedInstances.delete  
            
            !E fd_CMD_READ_ID
              developer info: "CVE-2015-3456, VENOM, fd_CMD_READ_ID attack vector"
              user info: "If SELinux is disabled, it's possible to proceed with the an successful attack."
              //<- sVirt
              <- svirt[SELinux]
              -> venomExploit
    
            & venomExploit
              -> executor.fullAccess
            
            | attemptVenomFDC @hidden
              -> venomExploit

            #patchStatus
             developer info: "Ensure the Qemu/KVM is patch up-to-date, and it's patched correctly"   
              -> bufferOverflow,
                fd_CMD_READ_ID,
                outOfBoundsReadORWrite,
                nullPointerDereference
             
    }

    asset Instance extends Object
           developer info: "Adapted from AWSLang, with minor changes. One instance is the  running on the machine" {
            
            | connect 
                developer info: "Attempt connection to the eg via shell, but the attacker has yet to authenticate"
                -> authenticatedAccess
        
            | authenticate
                developer info: "Does the attacker have the credentials to an account?."
                -> authenticatedAccess

            & authenticatedAccess
                developer info: "One way to get access to the machine is trough legitimate authentication."
                -> fullAccess


            | fullAccess
                developer info: "privileged user access, can read/Write/delete data, and stop an instance"
                ->  containedData.attemptRead,
                    containedData.attemptWrite,
                    containedData.attemptDelete, 
                    deviceEmulationExploit,
                    deny,
                    stop
            
            | stop
              developer info:"The instance is stopped /shutdown result is DOS"
              -> deny
            
            | delete
              developer info:"the instance is removed, since the instance uses ephemeral storage, removal of instance result in loss of data."
              -> containedData.attemptDelete

            | deviceEmulationExploit
             developer info: " The codebase in the QEMU quick emulator, stands for many of the exploits."
              -> improperMemoryBounds,
                outOfBoundsRead,
                nullPointerDereference
                
          
            | nullPointerDereference
              developer info: "CWE-476: NULL Pointer Dereference"
              -> attemptNullPointerDereference
            
            | outOfBoundsRead
              developer info: "CWE-125: Out-of-bounds Read"
            -> attemptExploitOutOfBoundsRead
            
            | improperMemoryBounds
              developer info: "CVE-2015-5158, CWE-119: Improper Restriction of Operations within the Bounds of a Memory Buffer"
              user info: "Exploits connected to: CVE-2015-5158, CVE-2015-7512, CVE-2015-7504, CVE-2017-10806, CVE-2017-10806, CVE-2016-3710"
              //user info: "deviceEmulationExploit -> [Vulnerability] -> [Exploit] -> hypervisor.<exploit>"
              -> attemptExploitBufferOverflow,
                venomFDC


            | attemptNullPointerDereference
              developer info: "AttackStep for CWE-476."
            -> hypervisor.nullPointerDereference
            
            | attemptExploitBufferOverflow
              developer info:"AttackStep for CWE-119."
              ->hypervisor.bufferOverflow
            
            | attemptExploitOutOfBoundsRead
              developer info:"AttackStep for CWE-125."
              ->hypervisor.outOfBoundsReadORWrite


            | venomFDC
              developer info: "CVE-2015-3456, VENOM"
              ->hypervisor.attemptVenomFDC

            | deny {A}
              -> guestSysExecutedApps.deny

    }

}

associations {

    
    //------------------------------- ### Nova/orchestrator related associations


    System [system]  1..*  <--  ExecutesSubSystems    -->    0..*  [subSystems]  System
      developer info: "Subsystems runs on the machine"

    //System [subSystems]  0..1  <--  SubSystemNovaCli    -->    0..1  [subSystem]  NovaService
    //  developer info: "NovaCli is a subsystem"
    
    //------------------------------- ### qemuKVM/System/Instance related associations
    System [executor] 1 <-- VirtHardware --> 0..1 [hypervisor] QemuKVM  
    developer info: "The system executes the Qemu virtulizer and the KVM converts the system kernel into a hypervisor."  


    QemuKVM [hypervisor] 0..1  <-- VirtHardware --> 1..* [sysExecutedInstances] Instance
    developer info: "Qemu-KVM handles I/O emulation CPU emulation and virtual hardware for each instance. Each instance have their own Qemu-process tied to the instance."

    NovaService [InstanceMGMT]  0..1  <--  OrchestratesInstancesViaLibvirt -->  1..*  [Instances]  QemuKVM
    developer info: "Nova compute orchestrates the KVM-QEMU instances."
   
   //------------------------------- ### Data related associations
    Data [containedData] * <-- InstanceContainment --> * [containingInstance]    Instance
    developer info: "An instance should be able to contain some data."

    Instance [guestExecutor]  0..* <-- SysExecution --> *  [guestSysExecutedApps] Application
    developer info: "The instance 'guestSystem' on which Applications are running."

   //------------------------------- ### LSM related associations
    //SELinux [sVirt] 0..1  <-- LinuxSecurityModule --> 0..1 [hypervisor]  QemuKVM
    //developer info: "SELinux provides MAC framework for the virtual machines"

    LSM [svirt] 0..1  <-- LinuxSecurityModule --> 0..1 [ONLYFORTEST]  QemuKVM
    developer info: "SELinux provides MAC framework for the virtual machines"


}
