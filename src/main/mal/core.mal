
category System {

    abstract asset Object
      developer info: "An object is the simplest form of an asset that can be compromised by a vulnerability."
    {
      | attemptUseVulnerability

      | deny {A}
      user info: "The Attacker can deny some or all functionality of an object"
          
    }

    


    asset System extends Object
    developer info: "Adapted from AWSLang"
    {      
        | connect 
           developer info: "Attempt connection to the eg via shell, but the attacker has yet to authenticate"
            -> attemptGainFullAccess
        
        | authenticate
           developer info: "Does the attacker have the credentials to an account?."
            -> attemptGainFullAccess

        & attemptGainFullAccess
            developer info: "One way to get access to the machine is trough legitimate authentication."
            -> fullAccess
    
        
        | fullAccess
            ->  _machineAccess

        | _machineAccess
          developer info: "if access to machine, its possible to read data from instances."
            ->  denialOfService,
                sysData.attemptAccess,
                subSystems[NovaService].attemptUseCLI
                
                                

        | denialOfService
            -> deny

        //# hardwareMemoryEncryption
        //  developer info: "Does not protect against Denial of service. https://developer.amd.com/wordpress/media/2013/12/AMD_Memory_Encryption_Whitepaper_v7-Public.pdf"
        //  user info: "To protect VM in runtime, hardware encryption is needed SME/SEV or MKTME. The encryption protects against the threat that the attacker has access to execute user privileged code or higher privileged code (or malware). on the hypervisor."
        //  ->  sysData.attemptRead,
        //      sysData.attemptWrite
        //      //sysData.delete : The attacker could still delete data if its encrypted

    }




asset Application extends Object
      developer info: "Adopted from Corelang. An application specifies pretty much everything that is executed or can execute other applications."
    {
      //No changes from CoreLang, Applications run on VM's
     
      | localConnect
        user info: "An attacker with low-privilege access on the executing instance is assumed to be able to locally (on the same host i.e. using loopback) interact with the application."
        ->  localAccess,
            specificAccessFromConnection
            //connectLocalInteraction

      | specificAccessFromConnection  @hidden
        developer info: "This intermediate step is used to represent that at least one type of connect has happened before being able to interact locally."
        -> specificAccess 
          //localInteraction

      | specificAccessFromIdentity  @hidden
        developer info: "This intermediate step is needed because if no LowApplicationPrivileges Identity is associated then localInteraction would be instantly compromised after connect"
        ->  specificAccess

      & specificAccess
        user info: "An attacker with low-privilege access on the executing instance is assumed to be able to locally (on the same host i.e. using loopback) interact with the executed applications."
        ->  appExecutedApps.localConnect // But also achieve localConnect on all child applications (this is something that needs to be reviewed again at a later stage)
            //attemptUseVulnerability   // Connection to all possible vulnerabilities that might be connected to the Application
            
      
      | networkConnect
        user info: "An attacker can connect to any network exposed application."
        ->  networkAccess,
            specificAccessFromConnection
            //attemptUseVulnerability // Connection to all possible vulnerabilities that might be connected to the Application

            
      | networkRequestConnect
        user info: "The attacker has successfully sent a request to the application."
        developer info: "Adopted from awsLang."
        ->  networkConnect

      | networkRespondConnect [Exponential(0.001)]
        user info: "An attacker may be able to respond to requests submitted by an application."
        developer info: "Adopted from awsLang."
        ->  networkConnect

      | authenticate
        user info: "The attacker is able to authenticate with the appropriate credentials."
        ->  localAccess,
            networkAccess

      & localAccess @hidden
        ->  fullAccess

      & networkAccess @hidden
        ->  fullAccess

      | fullAccess {C,I,A}
        user info: "Legitimate access, as user or as administrator."
        ->  read,
            modify,
            deny,
            appExecutedApps.fullAccess, // Gain access on all applications executed by this (host) application
            hostApp.localConnect    // and localConnect on the host application


      | codeExecution
        user info: "Vulnerabilities in the application could allow an attacker to inject code into the service process, thus assuming the service's privileges on the hosting instance. An attacker could under these circumstances influence other applications or the underlying operating system itself."
        developer info: "This attack steps specifically models the type of code injections which result in arbitrary code executions (e.g., buffer overflows), where the attacker can redirect the flow of execution, for example, by modifying program counter/instruction pointer of the host. (See CWE-94 on code injection <https://cwe.mitre.org/data/definitions/94.html> and, for example, CAPEC-100 on overflowing buffers <https://capec.mitre.org/data/definitions/100.html>). Adopted from awsLang."
        ->  fullAccess,
            modify

      | read {C}
        user info: "The attacker can read some or all of this service's code and data."
        developer info: "We don't model the services data, as we do not expect that information will be available to the parser. We also don't differentiate between service administrators and service users (e.g., mail service admins and users), as we have no information about the services. Adopted from awsLang."
        ->  containedData.attemptRead

      | modify {I}
        user info: "The attacker can modify some or all of this service's data. Adopted from awsLang."
        ->  containedData.attemptAccess

      | deny {A}
        user info: "The attacker can deny some or all functionality and data pertaining to this service. Adopted from awsLang."
        ->  containedData.deny

    }
}



category DataResources{

    asset Information
        user info: "Represents any type of information that might be contained inside Data."
    {
      | attemptAccess
        user info: "The attacker is attempting to access the information."
    }

    asset Data
        developer info: "Adopted from AWSlang, Encryption: https://docs.openstack.org/project-deploy-guide/openstack-ansible/draft/overview-storage-arch.html , Storage Nova: https://docs.openstack.org/project-deploy-guide/openstack-ansible/draft/overview-storage-arch.html"
        user info: "Any form of data that can be stored or transmitted. The data in a nova instance is not persistence, the disks associated with the VM's are ephermal, meaning from the users perspective they disappear when a VM is deleted."
    {
        | attemptAccess
          user info: "Attempt to access the data, this might fail if the dataNotExist defense is used."
          ->  access

        & access
          ->  attemptRead,
              attemptWrite,
              attemptDelete

        //!E dataEncrypted @hidden
        //  user info: "If the data are encrypted then accessing them requires the associated encryption credentials/key."
        //  <-  secureVirtualization[HardwareMemoryEncryption]
        //  ->  decryptData


          !E dataEncrypted @hidden
          user info: "If the data are encrypted then accessing them requires the associated encryption credentials/key."
          developer info: "Data will be considered as encrypted if there is at least one Credentials instance associated with it. Otherwise, 'accessUnencryptedData' is reached."
          <-  encryptCreds
          ->  accessUnencryptedData

        
        
          & accessUnencryptedData
          user info: "If data is unencrypted then access them."
          ->  accessDecryptedData


          | accessDecryptedData @hidden
          user info: "Intermediate attack step to only allow effects of 'accessUnencryptedData' on data after compromising the encryption credentials or encryption is disabled."
          ->  access,
              readContainedInformationAndData,
              read,
              write,
              delete
        
        
        //| decryptData
        //  ->  access,
        //      readContainedInformationAndData,
        //      read,
        //      write,
        //      delete
        
        # dataNotExist
          user info: "It models the probability of data actually not existing on the connected container (i.e. System, Application, Connection, etc.)."
          ->  access,
              readContainedInformationAndData,
              read,
              write,
              delete
    
        & readContainedInformationAndData
          user info: "From the data, attempt to access also the contained information/data, if exists."
          ->  information.attemptAccess,
              containedData.attemptAccess

        | attemptRead
          user info: "Attempt to read the data."
          ->  read

        | attemptWrite
          user info: "Attempt to write on the data."
          ->  write

        | attemptDelete
           user info: "Attempt to delete the data."
          -> delete

        & read {C}
          user info: "The attacker can read the data."
          ->  containedData.attemptRead,
              readContainedInformationAndData

        & write {I}
          user info: "The attacker can write to the location of the data, effectively deleting it."
          ->  containedData.attemptWrite,
              delete

        & delete {I,A}
          user info: "The attacker can delete the data."
          ->  containedData.attemptDelete

        | deny {A}
          user info: "if a DoS is performed data are denied, it has the same effects as deleting the data."
          -> containedData.deny



    }
}

category Networking {

  asset Network
      user info: "A network zone is a set of network accessible applications."
    {
      | physicalAccess {A}
        developer info: "Attacker has physical access on the network. This means he can cut wires/fibers and also connect using iLOs."
        ->  denialOfService


      | access
        user info: "Access provides connect to all reachable applications."
        ->  applications.networkConnect,
            denialOfService
            

      | denialOfService {A}
        user info: "If a DoS is performed it affects, the applications communicating over the network as well as the connected application."
        ->  applications.deny

    }

    


}

associations {

    //------------------------------- ### System/Application related associations

    Application [hostApp] 0..1 <-- AppExecution --> *   [appExecutedApps] Application
      developer info: "Application 1 runs application 2 runs application 3! (Where application 1 can be an OS, application 2 is a VM and application 3 is app running on the VM."
    
    //------------------------------- ### Data related associations
     Data [containingData] * <-- DataContainment --> * [containedData]   Data
      user info: "Data can be contained inside other data."
    
    Data [containedData] * <-- AppContainment --> * [containingApp]    Application
      developer info: "An application should be able to contain some data."

    System [system] 0..1 <-- DataHosting --> *  [sysData]   Data
      user info: "A system can host data."
    
    Data [containerData] * <-- InfoContainment --> *   [information]    Information
      user info: "Data can contain information, as for example credentials." 
    //------------------------------- ### Network related associations
      Network [networks]             * <-- NetworkExposure       --> *   [applications]           Application
      user info: "An application can communicate / be exposed on a network."

        Data  [encryptedData] * <-- EncryptionCredentials --> 0..1[encryptCreds]  HardwareMemoryEncryption
       user info: "Encrypted data can be associated with the relevant encryption credentials." 

}